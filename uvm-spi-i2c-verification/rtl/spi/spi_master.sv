`timescale 1ns / 1ps

module spi_master (
    input  logic       clk,
    input  logic       reset,

    input  logic       start,
    input  logic       cpol,
    input  logic       cpha,
    input  logic [7:0] clk_div,
    input  logic [7:0] tx_data,

    input  logic       miso,
    output logic       busy,
    output logic [7:0] rx_data,
    output logic       done,

    output logic       sclk,
    output logic       mosi,
    output logic       ss_n
);

    typedef enum logic [1:0] {
        IDLE  = 2'b00,
        START,
        DATA,
        STOP
    } spi_state_e;

    spi_state_e state;

    logic [7:0] div_cnt;
    logic [7:0] clk_div_r;
    logic       half_tick;

    logic [7:0] tx_shift_reg;
    logic [7:0] rx_shift_reg;
    logic [2:0] bit_cnt;

    logic       step;
    logic       cpol_r;
    logic       cpha_r;
    logic       sclk_r;

    assign sclk = sclk_r;

    always_ff @(posedge clk or posedge reset) begin
        if (reset) begin
            div_cnt   <= 8'd0;
            half_tick <= 1'b0;
        end else begin
            if (state == DATA) begin
                if (div_cnt == clk_div_r) begin
                    div_cnt   <= 8'd0;
                    half_tick <= 1'b1;
                end else begin
                    div_cnt   <= div_cnt + 1'b1;
                    half_tick <= 1'b0;
                end
            end else begin
                div_cnt   <= 8'd0;
                half_tick <= 1'b0;
            end
        end
    end



    always_ff @(posedge clk or posedge reset) begin
        if (reset) begin
            state        <= IDLE;
            mosi         <= 1'b1;
            ss_n         <= 1'b1;
            busy         <= 1'b0;
            done         <= 1'b0;

            tx_shift_reg <= 8'd0;
            rx_shift_reg <= 8'd0;
            bit_cnt      <= 3'd0;
            rx_data      <= 8'd0;

            step         <= 1'b0;
            cpol_r       <= 1'b0;
            cpha_r       <= 1'b0;
            clk_div_r    <= 8'd0;
            sclk_r       <= 1'b0;
        end else begin
            done <= 1'b0;

            case (state)

                IDLE: begin
                    mosi   <= 1'b1;
                    ss_n   <= 1'b1;
                    sclk_r <= cpol;
                    busy   <= 1'b0;
                    step   <= 1'b0;

                    if (start) begin
                        state        <= START;
                        cpol_r       <= cpol;
                        cpha_r       <= cpha;
                        clk_div_r    <= clk_div;
                        tx_shift_reg <= tx_data;
                        rx_shift_reg <= 8'd0;
                        bit_cnt      <= 3'd0;
                        busy         <= 1'b1;
                        ss_n         <= 1'b0;
                    end
                end



                START: begin
                    sclk_r <= cpol_r;

                    if (!cpha_r) begin
                        mosi         <= tx_shift_reg[7];
                        tx_shift_reg <= {tx_shift_reg[6:0], 1'b0};
                    end else begin
                        mosi <= 1'b1;
                    end

                    state <= DATA;
                end



                DATA: begin
                    if (half_tick) begin
                        sclk_r <= ~sclk_r;

                        if (step == 1'b0) begin
                            step <= 1'b1;

                            if (!cpha_r) begin
                                rx_shift_reg <= {rx_shift_reg[6:0], miso};
                            end else begin
                                mosi         <= tx_shift_reg[7];
                                tx_shift_reg <= {tx_shift_reg[6:0], 1'b0};
                            end

                        end else begin
                            step <= 1'b0;

                            if (!cpha_r) begin
                                if (bit_cnt == 3'd7) begin
                                    rx_data <= rx_shift_reg;
                                    state   <= STOP;
                                end else begin
                                    mosi         <= tx_shift_reg[7];
                                    tx_shift_reg <= {tx_shift_reg[6:0], 1'b0};
                                    bit_cnt      <= bit_cnt + 1'b1;
                                end

                            end else begin
                                rx_shift_reg <= {rx_shift_reg[6:0], miso};

                                if (bit_cnt == 3'd7) begin
                                    rx_data <= {rx_shift_reg[6:0], miso};
                                    state   <= STOP;
                                end else begin
                                    bit_cnt <= bit_cnt + 1'b1;
                                end
                            end
                        end
                    end
                end



                STOP: begin
                    sclk_r <= cpol_r;
                    ss_n   <= 1'b1;
                    mosi   <= 1'b1;
                    busy   <= 1'b0;
                    done   <= 1'b1;
                    state  <= IDLE;
                end

                default: begin
                    state <= IDLE;
                end

            endcase
        end
    end

endmodule