`timescale 1ns / 1ps

class transaction;
    rand bit [7:0] push_data;
    rand bit       push;
    rand bit       pop;
    bit      [7:0] pop_data;
    bit            full;
    bit            empty;

    int            test_mode;

    function debug_print(string name);
        $display(
            "%t : %s : push_data=%d,push=%d,pop=%d,pop_data=%d,full=%d,empty=%d"
            , $time, name, push_data, push, pop, pop_data, full, empty);
    endfunction
endclass




interface fifo_interface;
    logic       clk;
    logic       rst;
    logic [7:0] push_data;
    logic       push;
    logic       pop;
    logic [7:0] pop_data;
    logic       full;
    logic       empty;
endinterface






class generator;
    transaction            tr;
    mailbox #(transaction) gen2drv_mbox;
    int                    count           = 0;
    event                  event_gen_next;
    function new(mailbox#(transaction) gen2drv_mbox, event event_gen_next);
        this.gen2drv_mbox   = gen2drv_mbox;
        this.event_gen_next = event_gen_next;
    endfunction  //new(

    task run(int count);
        repeat (count) begin
            tr = new;
            tr.randomize();
            gen2drv_mbox.put(tr);
            @(event_gen_next);
        end
    endtask      
endclass  







class driver;
    transaction tr;
    mailbox #(transaction) gen2drv_mbox;
    event event_gen_next;
    virtual fifo_interface fifo_vif;


    function new(mailbox#(transaction) gen2drv_mbox, event event_gen_next,
                 virtual fifo_interface fifo_vif);
        this.gen2drv_mbox = gen2drv_mbox;
        this.event_gen_next = event_gen_next;
        this.fifo_vif = fifo_vif;
    endfunction  //new()

    task preset();
        fifo_vif.rst = 1;
        fifo_vif.push_data = 0;
        fifo_vif.push = 0;
        fifo_vif.pop = 0;
        @(posedge fifo_vif.clk);
        @(posedge fifo_vif.clk);
        fifo_vif.rst = 0;

        @(negedge fifo_vif.clk);
        
        assert(fifo_vif.empty) begin
        $display("");
        $display("**************************************");
        $display("   [DRV Assert] reset pass : empty!   ");
        $display("**************************************");
        $display("");
        end else begin $display("[DRV Assert] reset fail : empty = %d", fifo_vif.empty); end

        assert(!fifo_vif.full) begin
        $display("");
        $display("**************************************");
        $display("   [DRV Assert] reset pass : full!    ");
        $display("**************************************");
        $display("");
        end else begin $display("[DRV Assert] reset fail : full = %d", !fifo_vif.full); end
    endtask  //preset

    task push_only(int count);
        $display("");
        $display(" ******************************");
        $display(" | fifo push only test start | ");
        $display(" ******************************");
       // $display("");
        repeat (count) begin
            gen2drv_mbox.get(tr);
            @(posedge fifo_vif.clk);
            #1;
            fifo_vif.push = 1;
            fifo_vif.push_data = tr.push_data;
            fifo_vif.pop = 0;
            -> event_gen_next;
        end
    endtask

    task pop_only (int count);
        $display("");
        $display(" *****************************");
        $display(" | fifo pop only test start | ");
        $display(" *****************************");
        //$display("");
        repeat(count)begin
            gen2drv_mbox.get(tr);
            @(posedge fifo_vif.clk);
            #1;
            fifo_vif.pop  = 1;
            fifo_vif.push = 0;
            -> event_gen_next;
        end
    endtask

    task run();
        $display("");
        $display(" *******************************");
        $display(" | random push/pop test start | ");
        $display(" *******************************");
        $display("");
        forever begin
            gen2drv_mbox.get(tr);
            @(posedge fifo_vif.clk);
            #1;
            fifo_vif.push      = tr.push;
            fifo_vif.push_data = tr.push_data;
            fifo_vif.pop       = tr.pop;
        end
    endtask  
endclass  






class monitor;
    transaction tr;
    mailbox #(transaction) mon2scb_mbox;
    virtual fifo_interface fifo_vif;
    function new(mailbox#(transaction) mon2scb_mbox,
                 virtual fifo_interface fifo_vif);
        this.mon2scb_mbox = mon2scb_mbox;
        this.fifo_vif = fifo_vif;
    endfunction

    task run();
        forever begin
            @(negedge fifo_vif.clk);
            tr           = new;
            tr.push      = fifo_vif.push;
            tr.push_data = fifo_vif.push_data;
            tr.pop       = fifo_vif.pop;
            tr.pop_data  = fifo_vif.pop_data;
            tr.full      = fifo_vif.full;
            tr.empty     = fifo_vif.empty;
            mon2scb_mbox.put(tr);
        end
    endtask  
endclass  






class scoreboard;
    transaction tr;
    mailbox #(transaction) mon2scb_mbox;
    event event_gen_next;
    bit [7:0] fifo_queue [$:16];
    bit [7:0] compare_data;
    function new(mailbox#(transaction) mon2scb_mbox, event event_gen_next);
        this.mon2scb_mbox   = mon2scb_mbox;
        this.event_gen_next = event_gen_next;
    endfunction  

    task run();
        forever begin
            mon2scb_mbox.get(tr);
            if (tr.push && (!tr.full)) begin
                fifo_queue.push_front(tr.push_data);
            end
            if (tr.pop && (!tr.empty)) begin
                compare_data = fifo_queue.pop_back();
                if (tr.pop_data == compare_data) begin
                    $display("%t : *pass* , compare_data = %d", $time, compare_data);
                    tr.debug_print("scb");
                    $display("");
                end else begin
                    $display("%t : fail !! pop = %d, pop_data = %d, empty = %d",
                     $time, tr.pop, tr.pop_data, tr.empty);
                end
            end
        -> event_gen_next;
        end
    endtask  
endclass  







class environment;
    generator gen;
    driver drv;
    monitor mon;
    scoreboard scb;

    mailbox #(transaction) mon2scb_mbox;
    mailbox #(transaction) gen2drv_mbox;

    event event_gen_next;

    virtual fifo_interface fifo_vif;

    int  run_count;

    function new(virtual fifo_interface fifo_vif);
        gen2drv_mbox = new;
        mon2scb_mbox = new;

        gen = new(gen2drv_mbox, event_gen_next);
        drv = new(gen2drv_mbox, event_gen_next, fifo_vif);
        mon = new(mon2scb_mbox, fifo_vif);
        scb = new(mon2scb_mbox, event_gen_next);

        this.fifo_vif = fifo_vif;
    endfunction

    task run();
        drv.preset();
        //fifo interface initial
        run_count = 16;

        fork
            gen.run(run_count);
            drv.push_only(run_count);
        join
        @(posedge fifo_vif.clk);
        #1;
        $display(" | [ENV] push push_only test end | ");
        if(fifo_vif.full) begin
        $display(" |%t : full = %d", $time, fifo_vif.full);
        $display(" |%t : empty = %d", $time, fifo_vif.empty);
        $display(" |     pass: push only test      | ");
        $display("");
        end
        else $display("fail: push only test");
        #20;

        fork
            gen.run(run_count);
            drv.pop_only(run_count);
        join
        @(posedge fifo_vif.clk);
        #1;
        $display(" | [ENV] pop pop_only test end | ");
        if(fifo_vif.empty) begin
        $display(" |%t : empty = %d", $time,fifo_vif.empty);
        $display(" |%t : full = %d", $time, fifo_vif.full);
        $display(" |     pass: pop only test     | ");
        end
        else $display("fail: pop only test");
        #20;

        @(negedge fifo_vif.clk);
        fifo_vif.pop  = 0;
        fifo_vif.push = 0;

        fork 
            gen.run(100);
            drv.run();
            mon.run();
            scb.run();
        join_any
        #20;
        $display("            *** fifo constraint random test end ***");
        $display("");
        $display("");
        $stop;

    endtask
endclass




module tb_fifo_sv ();

    fifo_interface fifo_if ();
    environment env;

    fifo_sv dut (
        .clk(fifo_if.clk),
        .rst(fifo_if.rst),
        .push_data(fifo_if.push_data),
        .push(fifo_if.push),
        .pop(fifo_if.pop),
        .pop_data(fifo_if.pop_data),
        .full(fifo_if.full),
        .empty(fifo_if.empty)
    );

    always #5 fifo_if.clk = ~fifo_if.clk;

    initial begin
        fifo_if.clk = 0;
        env = new(fifo_if);
        env.run;
    end
endmodule
